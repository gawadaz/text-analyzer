terraform {
	required_providers {
		aws = {
			source  = "hashicorp/aws"
			version = ">= 5.0"
		}
	}
}

resource "aws_s3_bucket" "uploads" {
	bucket = var.bucket_name
	tags   = var.tags
}

resource "aws_s3_bucket_ownership_controls" "uploads" {
	bucket = aws_s3_bucket.uploads.id

	rule {
		object_ownership = "BucketOwnerEnforced"
	}
}

resource "aws_s3_bucket_public_access_block" "uploads" {
	bucket = aws_s3_bucket.uploads.id

	block_public_acls       = true
	block_public_policy     = true
	ignore_public_acls      = true
	restrict_public_buckets = true
}

resource "aws_s3_bucket_server_side_encryption_configuration" "uploads" {
	bucket = aws_s3_bucket.uploads.id

	rule {
		apply_server_side_encryption_by_default {
			sse_algorithm = "AES256"
		}
	}
}

resource "aws_s3_bucket_versioning" "uploads" {
	bucket = aws_s3_bucket.uploads.id

	versioning_configuration {
		status = var.enable_versioning ? "Enabled" : "Suspended"
	}
}

resource "aws_s3_bucket_cors_configuration" "uploads" {
	bucket = aws_s3_bucket.uploads.id

	cors_rule {
		allowed_headers = ["*"]
		allowed_methods = ["PUT", "GET", "HEAD"]
		allowed_origins = var.cors_allow_origins
		expose_headers  = ["ETag"]
		max_age_seconds = 3000
	}
}
